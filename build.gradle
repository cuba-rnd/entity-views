buildscript {
    ext.cubaVersion = '7.0.4'
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'https://dl.bintray.com/cuba-platform/main'
        }
        jcenter()
    }
    dependencies {
        classpath "com.haulmont.gradle:cuba-plugin:$cubaVersion"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1+'
    }
}

def modulePrefix = 'entity-views'
def artifactGlobalName = modulePrefix + "-global"
def artifactCoreName = modulePrefix + "-core"
def globalModule = project(artifactGlobalName)
def coreModule = project(artifactCoreName)

def artifactGroup = 'com.haulmont.addons.cuba.entity.views'
def artifactVersion = '0.1.1'
def isArtifactSnapshot = false

apply(plugin: 'cuba')

cuba {
    artifact {
        group = artifactGroup
        version = artifactVersion
        isSnapshot = isArtifactSnapshot
    }
}

dependencies {
    appComponent("com.haulmont.cuba:cuba-global:$cubaVersion")
}

def hsql = 'org.hsqldb:hsqldb:2.4.1'

configure([globalModule, coreModule]) {

    apply(plugin: 'java')
    apply(plugin: 'maven')
    apply(plugin: 'maven-publish')
    apply(plugin: 'cuba')
    apply(plugin: 'com.jfrog.bintray')

    dependencies {
        testCompile('junit:junit:4.12')
    }

    task sourceJar(type: Jar) {
        from file('src')
        classifier = 'sources'
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    javadocJar.dependsOn javadoc

    artifacts {
        archives sourceJar
        archives javadocJar
    }
}

configure(globalModule) {

    jar {
        manifest {
            attributes('App-Component-Id': cuba.artifact.group)
            attributes('App-Component-Version': cuba.artifact.version + (cuba.artifact.isSnapshot ? '-SNAPSHOT' : ''))
        }
    }

    publishing {
        publications {
            publishGlobal(MavenPublication) {
                groupId = artifactGroup
                artifactId = artifactGlobalName
                version = artifactVersion
                from components.java
                artifact sourceJar
                artifact javadocJar
            }
        }
    }


    bintray {
        publications = ['publishGlobal']
    }
}

configure(coreModule) {

    dependencies {
        compile(globalModule)
        testRuntime(hsql)
    }

    entitiesEnhancing {
        test {
            enabled = true
        }
    }
    publishing {
        publications {
            publishCore(MavenPublication) {
                groupId = artifactGroup
                artifactId = artifactCoreName
                version = artifactVersion
                from components.java
                artifact sourceJar
                artifact javadocJar
            }
        }
    }

    bintray {
        publications = ['publishCore']
    }
}

subprojects {
    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

        // make files public ?
        publish = true
        // override existing artifacts?
        override = true

        // metadata
        pkg {
            repo = 'labs'           // your repository name
            name = 'cuba-entity-views'  // package name - it will be created upon upload
            userOrg = 'cuba-platform'
            licenses = ["Apache-2.0"]
            websiteUrl = 'https://github.com/cuba-rnd/entity-views'
            issueTrackerUrl = 'https://github.com/cuba-rnd/entity-views/issues'
            vcsUrl = 'https://github.com/cuba-rnd/entity-views.git' // Mandatory for Open Source projects
            labels = ['cuba-platform', 'opensource']
            publicDownloadNumbers = true
        }
    }
}